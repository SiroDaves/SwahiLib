plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('keystore/key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.swahilib"
    compileSdk = 34
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.swahilib"
        minSdkVersion 24
        targetSdkVersion 34
        compileSdkVersion 34
        multiDexEnabled true
        versionCode 3
        versionName "1.0.0.3" 
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
        }
    }

    flavorDimensions "flavors"
    productFlavors { 
        production {
            dimension "flavors"
            applicationIdSuffix ""
            manifestPlaceholders = [appName: "Swahilib"]
        }
        staging {
            dimension "flavors"
            applicationIdSuffix ".stg"
            manifestPlaceholders = [appName: "Stg Swahilib"]
            versionCode 2
            versionName "1.0.0.1"
        }
        develop {
            dimension "flavors"
            applicationIdSuffix ".dev"
            manifestPlaceholders = [appName: "Dev Swahilib"]
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

flutter {
    source = "../.."
}
